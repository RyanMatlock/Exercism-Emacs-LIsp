#+title: Trinary

* First attempt

This passes:

#+begin_src emacs-lisp
  (defun trinary-to-decimal (string)
    "Convert STRING representing a trinary number to its decimal equivalent; if
  STRING contains invalid characters (i.e. other than 0, 1, or 2), return 0."
    (cond ((= (length string)
              (length (seq-filter #'(lambda (c) (or (= c ?0)
                                                    (= c ?1)
                                                    (= c ?2)))
                                  string)))
           (let ((base 3)
                 (power (1- (length string))))
             (apply #'+ (seq-mapn #'(lambda (n index)
                                      (* (string-to-number (string n))
                                         (expt 3 (- power index))))
                                  string
                                  (number-sequence 0 power)))))
          (t 0)))
#+end_src

But maybe I should use a regexp to validate the input.

#+begin_src emacs-lisp
  ELISP> (let ((invalid-trinary "102foo01"))
           (if (string-match-p "^[012]+$" invalid-trinary)
               :valid
             :invalid))
  :invalid
  ELISP> (let ((valid-trinary "10221211001101"))
           (if (string-match-p "^[012]+$" valid-trinary)
               :valid
             :invalid))
  :valid
#+end_src

* The community does it better
[[https://exercism.org/tracks/emacs-lisp/exercises/trinary/solutions/maurelio1234][maurelio1234's solution]]:

#+begin_src emacs-lisp
  (require 'cl-lib)

  (defun trinary-to-decimal (s)
    (if (string-match "[012]+$" s)
        (seq-reduce
         (lambda
           (acc c)
           (+ (* 3 acc) (- c ?0)))
         s
         0)
      0))
#+end_src

=seq-reduce= is nicer than =(apply #'+ (seq-mapn ...))=, doing the subtraction
on the char is nicer than =(string-to-number (string n))=, although I might've
done it as

#+begin_src emacs-lisp
  (defun char-to-num (c)
    (- c ?0))
#+end_src

just to make things more explicit (Pythonic habits die hard, and I'm not
playing code golf).
